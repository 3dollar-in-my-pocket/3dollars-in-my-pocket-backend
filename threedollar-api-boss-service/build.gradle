dependencies {
    implementation project(":threedollar-api-core:threedollar-api-core-service")
    implementation project(":threedollar-api-core:threedollar-api-core-listener")

    // TestFixtures
    testImplementation(testFixtures(project(":threedollar-domain:threedollar-domain-mongo")))
    testImplementation(testFixtures(project(":threedollar-domain:threedollar-domain-redis")))
    testImplementation(testFixtures(project(":threedollar-domain:threedollar-domain-rds")))
}

application {
    mainClass.set("com.depromeet.threedollar.api.bossservice.ThreeDollarApiBossApplicationKt")
}

bootJar {
    enabled = true
}
jar {
    enabled = true
}

ext {
    newRelicAgentDownloadUrl = "https://download.newrelic.com/newrelic/java-agent/newrelic-agent/current/newrelic-java.zip"
    newRelicExtraDirectory = "${buildDir}/newrelic"
    newRelicHostDirectory = "/usr/app/newrelic"
    newRelicJavaAgentDirectory = "${newRelicHostDirectory}/newrelic/newrelic.jar"
}

task downloadNewRelicAgent(type: Download) {
    src "${newRelicAgentDownloadUrl}"
    dest "${buildDir}/newrelic.zip"
}

task extractNewRelicAgent(type: Copy, description: "Extract Newrelic agent") {
    dependsOn downloadNewRelicAgent
    from zipTree(downloadNewRelicAgent.dest)
    into "${newRelicExtraDirectory}/${newRelicHostDirectory}"
}

jib {
    from {
        image = "adoptopenjdk/openjdk11:alpine-jre"
    }
    to {
        image = System.getProperty("image", "")
        tags = [System.getProperty("tag", "latest")]
    }
    extraDirectories.paths = [
        "${newRelicExtraDirectory}"
    ]
    container {
        jvmFlags = [
            "-server",
            "-XX:+UseG1GC",
            "-Xms256M",
            "-Xmx256M",
            "-javaagent:" + newRelicJavaAgentDirectory,
            "-Duser.timezone=Asia/Seoul"
        ]
        ports = ["4000"]
    }
}

tasks.jib {
    dependsOn extractNewRelicAgent
}
