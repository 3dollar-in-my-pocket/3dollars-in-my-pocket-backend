plugins {
    id "org.springframework.boot" version "2.5.14"
    id "io.spring.dependency-management" version "1.0.12.RELEASE"
    id "org.jetbrains.kotlin.jvm" version "1.6.21"
    id "org.jetbrains.kotlin.plugin.spring" version "1.6.21"
    id "application"
    id "jacoco"
    id "org.sonarqube" version "3.4.0.2513"
    id "com.google.cloud.tools.jib" version "3.2.1"
    id "de.undercouch.download" version "5.1.0"
}

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = "2020.0.4"
    swaggerVersion = "3.0.0"
    sentryVersion = "5.7.4"
    jacocoVersion = "0.8.8"
    springRetryVersion = "1.3.3"
    springmockkVersion = "3.1.1"
    springCloudAwsVersion = "2.2.6.RELEASE"
    embeddedRedisVersion = "0.7.2"
    embeddedMongoVersion = "3.4.5"
    commonsLang3Version = "3.12.0"
    commonsCodecVersion = "1.15"
    kotlinLoggingVersion = "2.1.20"
}

allprojects {
    group = "com.depromeet.threedollar"
}

subprojects {
    apply {
        plugin "idea"
        plugin "java-library"
        plugin "kotlin"
        plugin "org.springframework.boot"
        plugin "io.spring.dependency-management"
        plugin "org.jetbrains.kotlin.jvm"
        plugin "org.jetbrains.kotlin.plugin.spring"
        plugin "jacoco"
        plugin "java-test-fixtures"
        plugin "com.google.cloud.tools.jib"
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    compileKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        // Spring Boot
        implementation("org.springframework.boot:spring-boot-starter-validation")
        annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
        implementation("org.springframework.boot:spring-boot-starter-json")

        // Spring Retry
        implementation("org.springframework.retry:spring-retry:${springRetryVersion}")

        // Test
        testImplementation("org.springframework.boot:spring-boot-starter-test")

        // Lombok
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")
        testCompileOnly("org.projectlombok:lombok")
        testAnnotationProcessor("org.projectlombok:lombok")

        // Kotlin
        implementation("org.jetbrains.kotlin:kotlin-stdlib")
        implementation("org.jetbrains.kotlin:kotlin-reflect")

        // Kotlin Jackson
        implementation("com.fasterxml.jackson.module:jackson-module-kotlin")

        // Kotlin Logging
        implementation("io.github.microutils:kotlin-logging-jvm:${kotlinLoggingVersion}")

        // Spring MockK
        testImplementation("com.ninja-squad:springmockk:${springmockkVersion}")
    }

    sourceSets {
        integrationTest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            java.srcDirs(
                "src/integration-test/java",
                "src/integration-test/kotlin",
            )
            resources.srcDirs(
                "src/integration-test/resources",
            )
        }
        e2eTest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            java.srcDirs(
                "src/e2e-test/java",
                "src/e2e-test/kotlin",
            )
            resources.srcDirs(
                "src/e2e-test/resources",
            )
        }
    }

    idea {
        module {
            testSourceDirs += sourceSets.integrationTest.java.srcDirs
            testSourceDirs += sourceSets.integrationTest.kotlin.srcDirs
            testResourceDirs += sourceSets.integrationTest.resources.srcDirs

            testSourceDirs += sourceSets.e2eTest.java.srcDirs
            testSourceDirs += sourceSets.e2eTest.kotlin.srcDirs
            testResourceDirs += sourceSets.e2eTest.resources.srcDirs
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        integrationTestImplementation.extendsFrom implementation, testImplementation
        integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
        integrationTestCompileOnly.extendsFrom testCompileOnly
        integrationTestAnnotationProcessor.extendsFrom testAnnotationProcessor

        e2eTestImplementation.extendsFrom testImplementation
        e2eTestRuntimeOnly.extendsFrom testRuntimeOnly
        e2eTestCompileOnly.extendsFrom testCompileOnly
        e2eTestAnnotationProcessor.extendsFrom testAnnotationProcessor
    }

    tasks.withType(Test) {
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
    }

    task unitTest(type: Test, group: "verification", description: "Execute Unit Tests") {
        useJUnitPlatform()
        jacoco {
            destinationFile = file("${buildDir}/jacoco/unitTest.exec")
        }
    }

    task integrationTest(type: Test, group: "verification", description: "Execute Integration Tests") {
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        useJUnitPlatform()
        jacoco {
            destinationFile = file("${buildDir}/jacoco/integrationTest.exec")
        }
    }

    task e2eTest(type: Test, group: "verification", description: "Execute E2E Tests") {
        testClassesDirs = sourceSets.e2eTest.output.classesDirs
        classpath = sourceSets.e2eTest.runtimeClasspath
        useJUnitPlatform()
        jacoco {
            destinationFile = file("${buildDir}/jacoco/e2eTest.exec")
        }
    }

    task copyTestReport(type: Copy, description: "Copy Test Reports") {
        def testReportsDir = new File("${project.rootDir.absolutePath}/build/test-results")
        if (!testReportsDir.exists()) {
            testReportsDir.mkdirs()
        }
        from "${buildDir}/test-results"
        include "**/*.xml"
        into testReportsDir
    }

    task ci(type: Test, group: "verification", description: "CI") {
        dependsOn unitTest
        dependsOn integrationTest
        dependsOn e2eTest
        finalizedBy jacocoTestReport, copyTestReport
    }

    test {
        dependsOn unitTest
        dependsOn integrationTest
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "3dollar-in-my-pocket_3dollars-in-my-pocket-backend"
        property "sonar.organization", "3dollar-in-my-pocket"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/testCoverage/testCoverage.xml"
    }
}

jacoco {
    toolVersion = "${jacocoVersion}"
}

task testCoverage(type: JacocoReport, group: "verification", description: "Report Test Coverage") {
    executionData(fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec"))

    subprojects.forEach { project ->
        sourceSets(project.sourceSets.main as SourceSet)
    }

    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}
